COT1  EQU P3
COT2  EQU P1
	
HANG EQU P2
	
DK_COT1 EQU 30H
DK_COT2 EQU 31H
COT_BD  EQU 32H
	
NUT    BIT P0.0
NUT_LR BIT P0.1
	
BIT_PLAY BIT 0   ; DIA CHI BIT == 0 QUYET DINH PLAY=1 OR PAUSE=0
BIT_LR   BIT 1   ; DIA CHI BIT ==1: GIA TRI=1: CHU CHAY SANG PHAI, ==0: SANG TRAI
	
	MIN_TRAI EQU 0
	MAX_TRAI EQU 41
	
	MIN_PHAI EQU 41
	MAX_PHAI EQU 0
		
	VALUE_MIN EQU 33H
	VALUE_MAX EQU 34H
	
ORG 0
	MOV DPTR,#MA_LED   ; chuan bi vung du lieu
	SETB BIT_PLAY
	CLR  BIT_LR   ; BAN DAU SANG TRAI
	
	MOV VALUE_MIN, #MIN_TRAI
	MOV VALUE_MAX, #MAX_TRAI
	
	MOV COT_BD,#0
	MAIN:
		;DUNG NGUYEN LY QUET: MOI LAN SANG 1 COT: CAC COT CON LAI KO SANG
		;SANG CAC HANG NAO ??
		MOV R4,#11
		QUET1:
		MOV R3,#25
		QUET2:
			ACALL CHECK_BUTTON
			ACALL DISPLAY
		DJNZ R3,QUET2
		DJNZ R4,QUET1
		JNB BIT_PLAY, KO_DICH
			JNB BIT_LR, DICH_TRAI
			DICH_PHAI:
				DEC COT_BD
				MOV A,COT_BD
				CJNE A,#-1,RESET
					MOV COT_BD,#41
				RESET:
				JMP DICH_XONG
			DICH_TRAI:
				INC COT_BD
				MOV A,COT_BD
				CJNE A,#42,RESET2
					MOV COT_BD,#0
				RESET2:
			DICH_XONG:
		KO_DICH:
	JMP MAIN
	
	CHECK_BUTTON:  ; IF BUTTON PRESS: CHANGE BIT PLAY 0<=>1
		JB NUT, NUT_KO_BAM
			CPL BIT_PLAY    ; DAO BIT
			WAIT_NHA_NUT:
				ACALL DISPLAY
			JNB NUT, WAIT_NHA_NUT   ;DOI NHA BUTTON RA
		NUT_KO_BAM:
		
		JB NUT_LR, NUT_KO_BAM2
			CPL BIT_LR    ; DAO BIT
			WAIT_NHA_NUT2:
				ACALL DISPLAY
			JNB NUT_LR, WAIT_NHA_NUT2   ;DOI NHA BUTTON RA
		NUT_KO_BAM2:
	RET
	
	;HIEN THI 16 COT, TINH TU COT_BD  -> RA 8 COT CUA LED_MATRIX
	DISPLAY:
		MOV R1,COT_BD
		MOV COT2,#0    ; TAT COT2
		MOV DK_COT1,#10000000B  	  ; VI LED-MATRIX CHI CO 8 COT	
		MOV DK_COT2,#00000000B  	  ; VI LED-MATRIX CHI CO 8 COT	
		MOV R2,#16   ; LAP 16 LAN
		LAP_LAI:
			;LAY DC 1 COT
			MOV A,R1
			MOVC A,@A+DPTR
			MOV HANG, A      ; DUA MA LED RA 8 HANG DE HIEN THI
			MOV COT1,DK_COT1   ; DUA 8 BIT TRONG DK_COT -> PORT COT
			MOV COT2,DK_COT2   ; DUA 8 BIT TRONG DK_COT -> PORT COT
			NOP
			INC R1   ; NEXT COT
			CLR C
			MOV A,DK_COT1
			RRC A
			MOV DK_COT1,A
			MOV A,DK_COT2
			RRC A
			MOV DK_COT2,A
			MOV COT1,#00    ;TAT NO DI, TAT CA 8 COT
			MOV COT2,#00    ;TAT NO DI, TAT CA 8 COT
		DJNZ R2,LAP_LAI  ; LAP 16 LAN			
	RET

	; dinh nghia truoc du lieu cho 8 cot
	ma_led: 
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
		db 11111110b,	11111101b,	11111011b,	11110111b,	10101111b,	10011111b,	10001111b,	11111111b,	10001111b,	10001111b,	10001111b,	11111001b,	11111001b,	10111110b,	10000000b,	10111110b,	11111111b,	10000000b,	11111110b,	11111110b,	11000011b,	11111011b,	11000011b,	11111111b
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH



END