COT  EQU P3
HANG EQU P2
	
DK_COT EQU 30H
COT_BD EQU 31H
	
NUT    BIT P1.0
	
BIT_PLAY BIT 0   ; DIA CHI BIT == 0 QUYET DINH PLAY=1 OR PAUSE=0
	
ORG 0
	MOV DPTR,#MA_LED   ; chuan bi vung du lieu
	SETB BIT_PLAY
	
	MOV COT_BD,#32
	MAIN:
		;DUNG NGUYEN LY QUET: MOI LAN SANG 1 COT: CAC COT CON LAI KO SANG
		;SANG CAC HANG NAO ??
		MOV R4,#99
		QUET1:
		MOV R3,#25
		QUET2:
			ACALL CHECK_BUTTON
			ACALL DISPLAY
		DJNZ R3,QUET2
		DJNZ R4,QUET1
		JNB BIT_PLAY, KO_DICH
			DEC COT_BD
			MOV A,COT_BD
			CJNE A,#-1,RESET
				MOV COT_BD,#32
			RESET:
		KO_DICH:
	JMP MAIN
	
	CHECK_BUTTON:  ; IF BUTTON PRESS: CHANGE BIT PLAY 0<=>1
		JB NUT, NUT_KO_BAM
			CPL BIT_PLAY    ; DAO BIT
			WAIT_NHA_NUT:
				ACALL DISPLAY
			JNB NUT, WAIT_NHA_NUT   ;DOI NHA BUTTON RA
		NUT_KO_BAM:
	RET
	
	;HIEN THI 8 COT, TINH TU COT_BD  -> RA 8 COT CUA LED_MATRIX
	DISPLAY:
		MOV R1,COT_BD
		MOV DK_COT,#10000000B  	  ; VI LED-MATRIX CHI CO 8 COT	
		MOV R2,#8   ; LAP 8 LAN
		LAP_LAI:
			;LAY DC 1 COT
			MOV A,R1
			MOVC A,@A+DPTR
			MOV HANG, A      ; DUA MA LED RA 8 HANG DE HIEN THI
			MOV COT,DK_COT   ; DUA 8 BIT TRONG DK_COT -> PORT COT
			NOP
			MOV COT,#00    ;TAT NO DI, TAT CA 8 COT
			INC R1   ; NEXT COT
			MOV A,DK_COT
			RR A
			MOV DK_COT,A
		DJNZ R2,LAP_LAI  ; LAP 8 LAN	
	RET

	; dinh nghia truoc du lieu cho 8 cot
	ma_led: 
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
		db 11111110b,	11111101b,	11111011b,	11110111b,	10101111b,	10011111b,	10001111b,	11111111b,	10001111b,	10001111b,	10001111b,	11111001b,	11111001b,	10111110b,	10000000b,	10111110b,	11111111b,	10000000b,	11111110b,	11111110b,	11000011b,	11111011b,	11000011b,	11111111b
		DB 0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH



END